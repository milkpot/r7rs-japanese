
\extrapart{例} % -*- Mode: Lisp; Package: SCHEME; Syntax: Common-lisp -*-

\nobreak
手続き{\cf integrate-system}は微分方程式系
$$y_k^\prime = f_k(y_1, y_2, \ldots, y_n), \; k = 1, \ldots, n$$
をルンゲ=クッタ法で積分します。

パラメータ{\tt system-derivative}は
系の状態(状態変数 $y_1, \ldots, y_n$ に対する値のベクタ)を取り
系の微分係数(値 $y_1^\prime, \ldots, y_n^\prime$)を生成する関数です。
パラメータ{\tt initial-state}は系の初期状態を与え、
{\tt h}は積分ステップの長さの初期推定値です。

{\cf integrate-system}の戻り値は系の状態の無限ストリームです。

\begin{schemenoindent}
(define (integrate-system system-derivative
                          initial-state
                          h)
  (let ((next (runge-kutta-4 system-derivative h)))
    (letrec ((states
              (cons initial-state
                    (delay (map-streams next
                                        states)))))
      states)))%
\end{schemenoindent}

手続き{\cf runge-kutta-4}は系の状態から系の微分係数を生成する関数{\tt f}を取ります。
これは系の状態を取り新しい系の状態を生成する関数を生成します。

\begin{schemenoindent}
(define (runge-kutta-4 f h)
  (let ((*h (scale-vector h))
        (*2 (scale-vector 2))
        (*1/2 (scale-vector (/ 1 2)))
        (*1/6 (scale-vector (/ 1 6))))
    (lambda (y)
      ;; y is a system state
      (let* ((k0 (*h (f y)))
             (k1 (*h (f (add-vectors y (*1/2 k0)))))
             (k2 (*h (f (add-vectors y (*1/2 k1)))))
             (k3 (*h (f (add-vectors y k2)))))
        (add-vectors y
          (*1/6 (add-vectors k0
                             (*2 k1)
                             (*2 k2)
                             k3)))))))

(define (elementwise f)
  (lambda vectors
    (generate-vector
     (vector-length (car vectors))
     (lambda (i)
       (apply f
              (map (lambda (v) (vector-ref  v i))
                   vectors))))))

(define (generate-vector size proc)
  (let ((ans (make-vector size)))
    (letrec ((loop
              (lambda (i)
                (cond ((= i size) ans)
                      (else
                       (vector-set! ans i (proc i))
                       (loop (+ i 1)))))))
      (loop 0))))

(define add-vectors (elementwise +))

(define (scale-vector s)
  (elementwise (lambda (x) (* x s))))%
\end{schemenoindent}

{\cf map-streams}手続きは{\cf map}のストリーム版です。
第1引数(手続き)を第2引数(ストリーム)のすべての要素に適用します。

\begin{schemenoindent}
(define (map-streams f s)
  (cons (f (head s))
        (delay (map-streams f (tail s)))))%
\end{schemenoindent}

無限ストリームはcarがストリームの最初の要素を持ちcdrがストリームの残りを供給する
プロミスを持つペアとして実装されています。

\begin{schemenoindent}
(define head car)
(define (tail stream)
  (force (cdr stream)))%
\end{schemenoindent}

\bigskip
{\cf integrate-system}の使い方として減衰振動をモデル化した系
$$ C {dv_C \over dt} = -i_L - {v_C \over R}$$\nobreak
$$ L {di_L \over dt} = v_C$$
を積分する例を以下に示します。

\begin{schemenoindent}
(define (damped-oscillator R L C)
  (lambda (state)
    (let ((Vc (vector-ref state 0))
          (Il (vector-ref state 1)))
      (vector (- 0 (+ (/ Vc (* R C)) (/ Il C)))
              (/ Vc L)))))

(define the-states
  (integrate-system
     (damped-oscillator 10000 1000 .001)
     '\#(1 0)
     .01))%
\end{schemenoindent}

